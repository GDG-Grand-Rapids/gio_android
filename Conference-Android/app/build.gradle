apply plugin: 'android'


// Manifest version information!
def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    signingConfigs {

        debug {
            storeFile file("google_io_debug.jks")
            keyAlias "google_io"
            storePassword "googleio"
            keyPassword "googleio"
        }

        release {
            storeFile file("keystore.jks")
            storePassword "sagetech"
            keyAlias "sagetech"
            keyPassword "sagetech"
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_BASE_URL", "\"http://localhost:8080/api\""
        }
        release {
            buildConfigField "String", "API_BASE_URL", "\"http://178.0.2.3:8080/api\""
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.netflix.rxjava:rxjava-android:0.20.7'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.android.gms:play-services:4.0.30'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'


    compile fileTree(dir: 'libs', include: ['*.jar'])
}
