apply plugin: 'com.android.application'


// Manifest version information!
def versionMajor = 0
def versionMinor = 0
def versionPatch = 5
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))




android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }


    signingConfigs {

        debug {
            storeFile file("google_io_debug.jks")
            keyAlias "google_io"
            storePassword "googleio"
            keyPassword "googleio"
        }

        release {
            storeFile file("google_io.jks")
            keyAlias "google_io"
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_BASE_URL", "\"http://gioapi.gdggr.org\""
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "String", "API_BASE_URL", "\"http://gioapi.gdggr.org\""
            signingConfig signingConfigs.release
        }
    }
}

def runTasks = gradle.startParameter.taskNames
if ('assemble' in runTasks || 'assembleRelease' in runTasks) {
    def passwd = new String(System.console().readPassword('\nKeyStore Password: '))
    android.signingConfigs.release.storePassword = passwd
    android.signingConfigs.release.keyPassword = passwd
}

dependencies {
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.google.android.gms:play-services-auth:8.4.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
